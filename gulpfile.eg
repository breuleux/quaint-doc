
require-macros:
   earl-gulp -> task

require:
   @breuleux/engage-config
   quaint -> tools ->
      dedent, raw-relative
   quaint-javascript
   quaint-highlight
   node-static
   gulp
   http

opts = {
   hostname = "http://breuleux.github.io"
   root = "/quaint/"
   assets-root = "/quaint/assets/"
   default-language = null
   default-extension = ".html"
   default-image-extension = ".png"
   server-port = "3003"
   customize{@} =
      @register-macros with {
         .method{engine, spec, description} =
            {=> name, => args} = spec.extract{"\\name(\\maybe\\args)"}
            {
               engine.into{.sections, {2, code % name.raw{}, name.raw{}}}
               div.qmethod %
                  h2 %
                     id = name.raw{}
                     code % spec.raw{}
                  div.qmethod-description %
                     engine.gen{description}
            }
      }
      @register-rules with {
         "\\maybe\\rows && \\body"{engine, {=> body, => rows}} =
            q = quaint{quaint-javascript, quaint-highlight}
            b = dedent{raw-relative{body}}.trim{}
            div.quaint-example %
               div.input %
                  textarea %
                     rows = if{rows.raw{}.trim{}, rows.raw{}, b.split{"\n"}.length}
                     b
               div.output %
                  raw % q.toHTML{b} ;;, paragraph = true}
      }
}

task make:
   engage-config{opts}.start{}

task serve:
   s = new node-static.Server{"./output", {cache = false}}
   http.create-server{f}.listen{opts.server-port} where f{req, res} =
      req.add-listener{.end, f}.resume{} where f{} =
         s.serve{req, res}
   print 'Listening on port {opts.server-port}'

task default < {make, serve}
